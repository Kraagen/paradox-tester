apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'application'
apply plugin: 'codenarc'
apply plugin: 'maven-publish'

repositories {
    mavenCentral()
}

dependencies {
    compile 'log4j:log4j:1.2.+'
    compile 'org.codehaus.groovy.modules.http-builder:http-builder:0.7.+'
    compile 'org.codehaus.groovy:groovy-all:2.4.+'
    compile 'org.eclipse.jetty:jetty-servlet:9.3.6.+' //9.3.7.RC0 fails
    compile 'org.glassfish.jersey.containers:jersey-container-servlet-core:2.26-b03'
    testCompile 'org.testng:testng:6.9.+'
}

mainClassName = 'com.webtrends.qa.webtesting.MainClass'
jar.manifest.attributes 'Main-Class': mainClassName

applicationDistribution.from('config.groovy', 'webtesting.upstart') { }

test.useTestNG()

idea {
    project {
        jdkName = '1.8'
        vcs = 'Git'
    }
}

version = Boolean.parseBoolean(System.properties['release']) ? '1.0.1' : '2.0.0-SNAPSHOT'

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifactId rootProject.name
        }
    }

    repositories { maven { url "$buildDir/repo" } }
}

if (project.hasProperty('artifactoryPublish')) {
    artifactory {
        publish {
            repository {
                maven = true
            }
            defaults {
                publications('mavenJava')
                publishPom = true
            }
        }
    }
}

codenarc {
    toolVersion = '1.0'
    configFile = new File('config/codenarc/codenarc.groovy')
    maxPriority1Violations = 0
    maxPriority2Violations = 0
    maxPriority3Violations = Integer.MAX_VALUE
    reportFormat = project.properties.'codenarc.reportFormat' ?: 'html'
}

// This emulates what the rundeck job would do
task deployLocal(type: Exec, dependsOn: distZip) {
    executable "sudo"
    args 'sh', '-c', """
unzip -o $buildDir/distributions/webtesting.zip -d /opt/webtrends/
mv /opt/webtrends/webtesting/webtesting.upstart /etc/init/webtesting.conf
start webtesting || restart webtesting
""".toString()
}

task updateVersionTxt(type: Exec) {
    executable 'git'
    args 'log', '-n1', '--format=%h %ci', 'HEAD'
    standardOutput = new File('VERSION.txt').newOutputStream()
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.1'
}
